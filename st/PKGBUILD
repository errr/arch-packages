# Based on https://aur.archlinux.org/packages/st-git-zsh/ by Michele Sr <mikefender@cryptolab.net>

pkgname=st-xxx
pkgver=20160811.6e79e83
pkgrel=1
pkgdesc='Simple terminal implementation for X (as mod)'
url='http://st.suckless.org/'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft' 'fontconfig' 'otf-fira-code')
makedepends=('ncurses' 'libxext' 'git')
provides=('st')
conflicts=('st')
source=('git://git.suckless.org/st#tag=0.7'
        config.patch
        hidecursor.patch
        vertcenter.patch)
sha1sums=('SKIP'
          'aa94bb9c72c5950c77efdc23938a1d7a7f354002'
          'd2e764c601549f44909edbc2d982d303781f95d8'
          'dcde03cab595bdacc51011bcfc5d520611b9a03f')


pkgver() {
  cd "${srcdir}/st"
  git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
  cd "${srcdir}/st"
  sed -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
      -e 's/CFLAGS =/CFLAGS +=/g' \
      -e 's/LDFLAGS =/LDFLAGS +=/g' \
      -i config.mk
  patch -p1 -i "${srcdir}/config.patch"
  patch -p1 -i "${srcdir}/hidecursor.patch"
  patch -p1 -i "${srcdir}/vertcenter.patch"
}

build() {
  cd "${srcdir}/st"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/st"
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
