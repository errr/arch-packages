pkgname=st-xxx
pkgver=0.8.1
pkgrel=1
pkgdesc='Simple terminal implementation for X.'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext')
makedepends=('ncurses')
provides=('st')
conflicts=('st')
source=(https://dl.suckless.org/st/st-$pkgver.tar.gz
        config.patch
        hidecursor.patch)
sha256sums=('c4fb0fe2b8d2d3bd5e72763e80a8ae05b7d44dbac8f8e3bb18ef0161c7266926'
            'fe36fd852d4b570739e50ab3e8a351903152f976266cd9336bb72076d8dbe931'
            'b330af3d9934587549636bc8db4b5d6d9d8eed0e323afd29283dcdec38a3dfc1')

prepare() {
  cd "${srcdir}/st-$pkgver"
  patch -p1 -i "${srcdir}/config.patch"
  patch -p1 -i "${srcdir}/hidecursor.patch"
}

build() {
  cd "${srcdir}/st-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/st-$pkgver"
  ticdir="${pkgdir}/etc/terminfo"
  mkdir -p "$ticdir"
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="$ticdir" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
