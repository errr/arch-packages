# Based on https://aur.archlinux.org/packages/st-git-zsh/ by Michele Sr <mikefender@cryptolab.net>

pkgname=st-xxx
pkgver=20170410.5a10aca
pkgrel=1
pkgdesc='Simple terminal implementation for X (as mod)'
url='http://st.suckless.org/'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft' 'fontconfig' 'otf-fira-mono')
makedepends=('ncurses' 'libxext' 'git')
provides=('st')
conflicts=('st')
source=('git://git.suckless.org/st'
        config.patch
        hidecursor.patch)
sha1sums=('SKIP'
          'a1cbfa391780ae79bc467b0e64af363d4b958a48'
          'b85e06f6190a55c2754fd47a1ff3f3dfd74722f6')

pkgver() {
  cd "${srcdir}/st"
  git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
  cd "${srcdir}/st"
  sed -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
      -e 's/CFLAGS =/CFLAGS +=/g' \
      -e 's/LDFLAGS =/LDFLAGS +=/g' \
      -i config.mk
  patch -p1 -i "${srcdir}/config.patch"
  patch -p1 -i "${srcdir}/hidecursor.patch"
}

build() {
  cd "${srcdir}/st"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/st"
  ticdir="${pkgdir}/etc/terminfo"
  mkdir -p "$ticdir"
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="$ticdir" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
