# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com

pkgname=gnome-shell-xxx
pkgver=3.28.1
pkgrel=1
pkgdesc="The next generation GNOME Shell"
url="https://wiki.gnome.org/Projects/GnomeShell"
arch=(x86_64)
license=(GPL2)
depends=(accountsservice gcr gjs gnome-bluetooth upower gnome-session gnome-settings-daemon
         gnome-themes-extra gsettings-desktop-schemas libcanberra-pulse libcroco libgdm libsecret
         mutter nm-connection-editor unzip gstreamer libibus)
makedepends=(gtk-doc gnome-control-center evolution-data-server gobject-introspection git meson
             sassc)
optdepends=('gnome-control-center: System settings'
            'evolution-data-server: Evolution calendar integration')
groups=(gnome)
provides=('gnome-shell')
conflicts=('gnome-shell')
_commit=5b10d157fe5b60a11e87737c1050f161b2f986a6  # tags/3.28.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-shell.git#commit=$_commit"
        "git+https://git.gnome.org/browse/libgnome-volume-control"
        xxx.patch)
sha256sums=('SKIP'
            'SKIP'
            '4a23db8d115c95eb845e5893b930cd06f1adf1ec51a01fbd1163fbb71baf19e3')

pkgver() {
  cd gnome-shell
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gnome-shell

  # Move the plugin to our custom epiphany-only dir
  sed -i "s/'mozilla'/'epiphany'/g" meson.build

  git submodule init
  git config --local submodule.subprojects/gvc.url "$srcdir/libgnome-volume-control"
  git submodule update

  patch -Np1 -i ../xxx.patch
}
  
build() {
  arch-meson gnome-shell build -D gtk_doc=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # Must exist; FS#37412
  mkdir "$pkgdir/usr/share/gnome-shell/modes"
}
